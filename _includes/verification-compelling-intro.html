
<div class="step-through">
<div class="step">
<div class="file" name="euclidian.dfy">
{% highlight javascript linenos %}
method EuclidianDiv(a: int, b: int) returns (q: int, r: int) {
  if a <= b {
    q := 0;
    r := a;
  } else {
    var q', r' := EuclidianDiv(a - b, b);
    q := q' + 1;
    r := r';
  }
}
method Main() {
  var a, b := 47, 13;
  var q, r := EuclidianDiv(a, b);
  print a, " = ", q, "*", b, " + ", r;
}
{% endhighlight %}
</div><!--.file-->
If you run <code><span class="command">dafny run --no-verify euclidian.dfy</span><!--.command--></code>, you get the following output:
<!--The following is tested, and can also be overriden -->
<pre class="output">
Dafny program verifier did not attempt verification
47 = 3*13 + 8
</pre><!--.output-->
<p>If you were to run code coverage, you'd have 100% with your function. However, bugs can lurk in many more places... let's remove the <code>--no-verify</code> option. Click <button>Next</button>.</p>
</div><!--.step-->
<div class="step">
<div class="file" name="euclidian.dfy">
{% highlight javascript linenos %}
method EuclidianDiv(a: int, b: int) returns (q: int, r: int) {
  if a <= b {
    q := 0;
    r := a;
  } else {
    var q', r' := EuclidianDiv(a - b, b);
    q := q' + 1;
    r := r';
  }
}
method Main() {
  var a, b := 47, 13;
  var q, r := EuclidianDiv(a, b);
  print a, " = ", q, "*", b, " + ", r;
}
{% endhighlight %}
</div><!--.file-->
<div class="explanation">
Without changing the code above, we now remove the <code>--no-verify</code> option (and add <code>--show-snippets</code> so that it gives a prettier output).
</div>
By running <code><span class="command">dafny run --show-snippets euclidian.dfy</span><!--.command--></code>, you now get the following output:
<!--The following is tested, and can also be overriden -->
<pre class="output">
euclidian.dfy(6,15): Error: cannot prove termination; try supplying a decreases clause
  |
6 |     var q', r' := EuclidianDiv(a - b, b);
  |                ^^^^^^^^^^^^^^^^^^^^^^^^^^

euclidian.dfy(6,15): Error: decreases expression at index 0 must be bounded below by 0
  |
6 |     var q', r' := EuclidianDiv(a - b, b);
  |                ^^^^^^^^^^^^^^^^^^^^^^^^^^

euclidian.dfy(1,20): Related location
  |
1 | method EuclidianDiv(a: int, b: int) returns (q: int, r: int) {
  |                     ^


Dafny program verifier finished with 1 verified, 2 errors
</pre><!--.output-->
<p>Dafny could not prove that the method <em>terminates</em>. Wait a second, does it?
You think for a second, reasons like "Well, 'a' decreases, and once it's smaller
than 'b', it terminates. And 'a' is always greater or equal to zero, but how do we tell Dafny?
Let's revisit the code. Click <button class="top">Next</button>.</p>
</div><!--.step-->
<div class="step">
<div class="file" name="euclidian.dfy">
{% highlight javascript linenos mark_lines="2 3" %}
method EuclidianDiv(a: int, b: int) returns (q: int, r: int)
  requires 0 <= a && b != 0  // Just added
  decreases a                // Just added
{
  if a <= b {
    q := 0;
    r := a;
  } else {
    var q', r' := EuclidianDiv(a - b, b);
    q := q' + 1;
    r := r';
  }
}
method Main() {
  var a, b := 47, 13;
  var q, r := EuclidianDiv(a, b);
  print a, " = ", q, "*", b, " + ", r;
}
{% endhighlight %}
</div><!--.file-->
<div class="explanation">
You added a <code>requires</code> clause to your method to ensure that
<code>EuclidianDiv</code> is not called with a negative <code>a</code>.
and also that <code>b</code> is not zero.
You also add a <code>decreases a</code> clause, and Dafny verifies that the expression <code>a</code> decreases when calling itself recursively, and is greater or equal to zero.
Clauses are checked statically by Dafny but not compiled nor executed, like a clever
type check, which is pretty nice.
</div>
If you run <code><span class="command">dafny run --show-snippets euclidian.dfy</span><!--.command--></code>, you get the following output:
<!--The following is tested, and can also be overriden -->
<pre class="output">
euclidian.dfy(9,15): Error: decreases clause might not decrease
  |
9 |     var q', r' := EuclidianDiv(a - b, b);
  |                ^^^^^^^^^^^^^^^^^^^^^^^^^^


Dafny program verifier finished with 1 verified, 1 error
</pre><!--.output-->
<p>Well, it seems that Dafny does not believe that <code>a - b</code> is less than <code>a</code>. Time for an assertion.
Assertions are like clauses, they are not compiled nor executed, Dafny will just check them during verification. Click <button class="top">Next</button>.
</p>
</div><!--.step-->
<div class="step">
<div class="file" name="euclidian.dfy">
{% highlight javascript linenos mark_lines="9" %}
method EuclidianDiv(a: int, b: int) returns (q: int, r: int)
  requires 0 <= a && b != 0
  decreases a
{
  if a <= b {
    q := 0;
    r := a;
  } else {
    assert a - b < a;        // Just added
    var q', r' := EuclidianDiv(a - b, b);
    q := q' + 1;
    r := r';
  }
}
method Main() {
  var a, b := 47, 13;
  var q, r := EuclidianDiv(a, b);
  print a, " = ", q, "*", b, " + ", r;
}
{% endhighlight %}
</div><!--.file-->
If you run <code><span class="command">dafny run --show-snippets euclidian.dfy</span><!--.command--></code>, you get the following output:
<!--The following is tested, and can also be overriden -->
<pre class="output">
euclidian.dfy(9,11): Error: assertion might not hold
  |
9 |     assert a - b < a;        // Just added
  |            ^^^^^^^^^


Dafny program verifier finished with 1 verified, 1 error
</pre><!--.output-->
<p>You start to realize that something is wrong. In your head, you see that
"a - b &lt; a" is the same as "-b &lt; 0", which is the same as "b > 0". Surprise! You just found out that,
if you accidentally called the function with a negative 'b', the function would loop forever! Click <button class="top">Next</button> to correct that.
</p>
</div><!--.step-->
<div class="step">
<div class="file" name="euclidian.dfy">
{% highlight javascript linenos mark_lines="2" %}
method EuclidianDiv(a: int, b: int) returns (q: int, r: int)
  requires 0 <= a && 0 < b    // Just modified to use '<' instead of '!='
  decreases a
{
  if a <= b {
    q := 0;
    r := a;
  } else {
    assert a - b < a;
    var q', r' := EuclidianDiv(a - b, b);
    q := q' + 1;
    r := r';
  }
}
method Main() {
  var a, b := 47, 13;
  var q, r := EuclidianDiv(a, b);
  print a, " = ", q, "*", b, " + ", r;
}
{% endhighlight %}
</div><!--.file-->
<div class="explanation">
You modified the <code>requires</code> clause to your method to ensure that
<code>EuclidianDiv</code> is not called with a negative <code>b</code>
</div>
If you run <code><span class="command">dafny run --show-snippets euclidian.dfy</span><!--.command--></code>, you finally get it working:
<!--The following is tested, and can also be overriden -->
<pre class="output">
Dafny program verifier finished with 2 verified, 0 errors
47 = 3*13 + 8
</pre><!--.output-->
<p>By turning on verification, you were able to fix a bug that your test coverage would not have spotted.
But Dafny does not stop here. Can you prove that you did the right thing, which is computing the quotient and remainder?
Let's figure out. We can add one <code>ensures</code> clause to verify a relationship between inputs and outputs. Click  <button class="top">Next</button> to proceed.
</p>
</div><!--.step-->
<div class="step">
<div class="file" name="euclidian.dfy">
{% highlight javascript linenos mark_lines="4 5" %}
method EuclidianDiv(a: int, b: int) returns (q: int, r: int)
  requires 0 <= a && 0 < b
  decreases a
  ensures a == b * q + r  // Just added
  ensures 0 <= r < b      // Just added
{
  if a <= b {
    q := 0;
    r := a;
  } else {
    assert a - b < a;
    var q', r' := EuclidianDiv(a - b, b);
    q := q' + 1;
    r := r';
  }
}
method Main() {
  var a, b := 47, 13;
  var q, r := EuclidianDiv(a, b);
  print a, " = ", q, "*", b, " + ", r;
}
{% endhighlight %}
</div><!--.file-->
<div class="explanation">
You added one <code>ensures</code> clause to your method to ensure that
<code>EuclidianDiv</code> actually returns what it is supposed to compute,
given some properties you know of Euclidian division.
Note that Dafny lets you write chaining comparisons. How many languages allow that?
</div>
If you now run <code><span class="command">dafny run --show-snippets euclidian.dfy</span><!--.command--></code>, you get the following result:
<!--The following is tested, and can also be overriden -->
<pre class="output">
euclidian.dfy(6,0): Error: a postcondition could not be proved on this return path
  |
6 | {
  | ^

euclidian.dfy(5,15): Related location: this is the postcondition that could not be proved
  |
5 |   ensures 0 <= r < b      // Just added
  |                ^^^^^


Dafny program verifier finished with 1 verified, 1 error
</pre><!--.output-->
<p>From this, you see that Dafny was not able to prove that <code>r &lt; a</code> always hold.
To identify why it is the case, you could apply techniques of <a href="https://dafny.org/dafny/DafnyRef/DafnyRef#sec-verification-debugging">verification debugging</a>. For now, you just realize your mistake:
It's only if <code>a &lt; b</code> that you should take the <codee>if</codee> branch. When <code>a = b</code>, you should
take one more step recursively. Click <button class="top">Next</button> to fix the code.
</p>
</div><!--.step-->
<div class="step">
<div class="file" name="euclidian.dfy">
{% highlight javascript linenos mark_lines="7" %}
method EuclidianDiv(a: int, b: int) returns (q: int, r: int)
  requires 0 <= a && 0 < b
  decreases a
  ensures a == b * q + r
  ensures 0 <= r < b
{
  if a < b {             // Fixed. Was previously 'a <= b'
    q := 0;
    r := a;
  } else {
    assert a - b < a;
    var q', r' := EuclidianDiv(a - b, b);
    q := q' + 1;
    r := r';
  }
}
method Main() {
  var a, b := 47, 13;
  var q, r := EuclidianDiv(a, b);
  print a, " = ", q, "*", b, " + ", r;
}
{% endhighlight %}
</div><!--.file-->
<div class="explanation">
You modified your condition to stop recursing only if <code>a &lt; b</code>.
</div>
If you run <code><span class="command">dafny run euclidian.dfy</span><!--.command--></code>, you get the following result:
<!--The following is tested, and can also be overriden -->
<pre class="output">
Dafny program verifier finished with 2 verified, 0 errors
47 = 3*13 + 8
</pre><!--.output-->
<p>Well done. You not only got it working, but you also proved that your implementation satisfies
a high-level specification.
</p>
</div><!--.step-->
</div><!--.step-through-->
